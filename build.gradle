plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.google.cloud.artifactregistry.gradle-plugin' version '2.2.0'
    id 'com.github.sherter.google-java-format' version '0.9'
}

repositories {
    mavenCentral()
    maven {
        url "https://us-east1-maven.pkg.dev/provision-general-operations/provision-public"
    }
}

googleJavaFormat {}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
group = 'com.exclamationlabs.connid'

def build_version = System.getenv('BUILD_NUMBER') == null ? System.currentTimeMillis() : System.getenv('BUILD_NUMBER')
project.version="${software_version}-${build_version}"

dependencies {

    ////// Begin - Vital dependencies for Base Connector
    api 'org.apache.commons:commons-lang3:3.12.0'
    api 'net.tirasa.connid:connector-test-common:1.5.0.0'
    api 'com.auth0:java-jwt:3.19.4'

    api ("org.springframework.boot:spring-boot-starter-validation:2.7.16") {
        exclude group: "org.yaml", module: "snakeyaml"

    }
    api 'org.yaml:snakeyaml:2.0'
    ////// End - Vital dependencies for Base Connector

    // HTTP/RESTful support
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'

    implementation 'commons-codec:commons-codec:1.15'

    // PEM Private key support
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.76'

    // SOAP support
    // implementation 'com.sun.xml.ws:jaxws-ri:2.3.2'

    testImplementation "com.exclamationlabs.connid:connector-base-test-support:${test_connector_version}-+"
    testImplementation 'net.tirasa.connid:connector-framework-internal:1.5.0.0'

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
}



task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'source'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

task copyPom(type: Copy) {
    from 'build/publications/connector_base/pom-default.xml'
    into 'build/libs'
    rename ('pom-default.xml', "${project.name}-${project.version}.pom")
}

publishing {
    publications {
        connector_base(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom {
                name = "Connector Base: Framework"
                description = "Base Connector Framework"
                url = "https://github.com/ExclamationLabs/connector-base"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    url = "https://github.com/ExclamationLabs/connector-base/tree/master"
                }
            }
        }
    }
    repositories {
        maven {
            url "artifactregistry://us-east1-maven.pkg.dev/provision-general-operations/provision-public/"
        }
    }
}

test {
    useJUnitPlatform() {}
}

build.dependsOn(copyPom)
copyPom.dependsOn(publishToMavenLocal)

