plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven {
        url "https://artifactory.exclamationlabs.com/artifactory/libs-release-local"
        credentials {
                username = System.getenv('ARTIFACTORY_GRADLE_USER')
                password = System.getenv('ARTIFACTORY_GRADLE_PASS')
            }
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
group = 'com.exclamationlabs.connid'

def build_version = System.getenv('BUILD_NUMBER') == null ? 'SNAPSHOT' : System.getenv('BUILD_NUMBER')
project.version="${software_version}-${build_version}"

dependencies {

    ////// Begin - Vital dependencies for Base Connector
    api 'org.apache.commons:commons-lang3:3.8.1'
    api 'net.tirasa.connid:connector-test-common:1.5.0.0'
    api 'com.auth0:java-jwt:3.10.3'

    api 'org.springframework.boot:spring-boot-starter-validation:2.5.14'
    ////// End - Vital dependencies for Base Connector

    // HTTP/RESTful support
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.apache.httpcomponents:httpclient:4.5.10'

    // PEM Private key support
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.58'

    testImplementation "com.exclamationlabs.connid:connector-base-test-support:2.0.14.1-+"
    testImplementation 'net.tirasa.connid:connector-framework-internal:1.5.0.0'
}



task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'source'
}

task copyPom(type: Copy) {
    from 'build/publications/connector_base/pom-default.xml'
    into 'build/libs'
    rename ('pom-default.xml', "${project.name}-${project.version}.pom")
}

publishing {
    publications {
        connector_base(MavenPublication) {
            from components.java
            artifact sourceJar
            pom {
                name = "Connector Base: Framework"
                description = "Base Connector Framework"
                url = "https://github.com/ExclamationLabs/connector-base"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    url = "https://github.com/ExclamationLabs/connector-base/tree/master"
                }
            }
        }
    }
}

build.dependsOn(copyPom)
copyPom.dependsOn(publishToMavenLocal)

