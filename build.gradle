plugins {
    id 'java'
    id 'application'
}

repositories {
    jcenter()
}

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.8.1'
    compile 'net.tirasa.connid:connector-test-common:1.5.0.0'

    compile 'org.codehaus.groovy:groovy:2.5.2'

    // HTTP/RESTful support
    compile 'com.google.code.gson:gson:2.8.6'
    compile 'org.apache.httpcomponents:httpclient:4.5.10'

    // JWT.io auth API for JWT
    compile 'com.auth0:java-jwt:3.10.3'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.1.0'
    testImplementation 'net.tirasa.connid:connector-framework-internal:1.5.0.0'
}

task copyExternalJar(type: Copy) {
   from file("externalLib/connector-common-1.5.0.0.jar")
   into file("custom/lib")
}

task copyDependencyJars(type: Copy, dependsOn: 'copyExternalJar') {
    from configurations.compile.collect {
        if(it.isFile() && it.getCanonicalFile().getCanonicalPath().endsWith('.jar') &&
                (!it.getCanonicalFile().getCanonicalPath().contains("connector-framework"))) {
            it
        }
    }
    into file("custom/lib")
}

test {
    exclude '**/*IntegrationTest*'
}

task baseClean(type: Delete) {
    delete 'custom/lib'
}

task toJar(type: Jar, dependsOn:['copyDependencyJars']) {
    archiveName('connector-base-zoom-0.1-connector.jar')
    from 'custom', 'build/classes/java/main', 'src/main/resources'

    manifest {
        attributes("ConnectorBundle-FrameworkVersion": "1.4.3.11",
                "ConnectorBundle-Version": "0.1",
                "ConnectorBundle-Name": "connector-base-zoom")
    }
}

clean.dependsOn('baseClean')

jar.dependsOn('toJar')