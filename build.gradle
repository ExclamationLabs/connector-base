plugins {
    id 'java-library'
    id 'maven'
}

repositories {
   jcenter()
    maven {
        url "https://artifactory.exclamationlabs.com/artifactory/libs-release-local"
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
group = 'com.exclamationlabs.connid'

def build_version = System.getenv('BUILD_NUMBER') == null ? 'SNAPSHOT' : System.getenv('BUILD_NUMBER')
project.version="${software_version}-${build_version}"

dependencies {

    ////// Begin - Vital dependencies for Base Connector
    api 'org.apache.commons:commons-lang3:3.8.1'
    api 'net.tirasa.connid:connector-test-common:1.5.0.0'
    api 'com.auth0:java-jwt:3.10.3'
    ////// End - Vital dependencies for Base Connector

    // HTTP/RESTful support
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.apache.httpcomponents:httpclient:4.5.10'

    // PEM Private key support
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.58'

    testImplementation 'com.exclamationlabs.connid:connector-base-test-support:0.1-+'
    testImplementation 'net.tirasa.connid:connector-framework-internal:1.5.0.0'
}


task generatePom {
    doLast {
        pom().writeTo("build/libs/${project.name}-${project.version}.pom")
    }
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')
javadoc.options.addStringOption('source', '8')
javadoc.options.addStringOption('link', 'https://docs.oracle.com/javase/8/docs/api/')

task storeJavadoc(type: Copy, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    into 'docs'
}

task cleanJavadoc(type: Delete) {
    delete 'docs'
}

clean.dependsOn(cleanJavadoc)
build.dependsOn(generatePom, storeJavadoc)
