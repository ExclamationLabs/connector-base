plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.google.cloud.artifactregistry.gradle-plugin' version '2.2.0'
    id "com.diffplug.spotless" version "6.25.0"
}

repositories {
    mavenCentral()
    maven {
        url "https://us-east1-maven.pkg.dev/provision-general-operations/provision-public"
    }
}

java {
    registerFeature('jwt') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('cryptography') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('http') {
        usingSourceSet(sourceSets.main)
    }
}

sourceCompatibility = '1.11'
targetCompatibility = '1.11'
group = 'com.exclamationlabs.connid'

def rc_version = System.getenv('RELEASE_CANDIDATE') == null || System.getenv('RELEASE_CANDIDATE') == "" ? "" : "-rc.${System.getenv('RELEASE_CANDIDATE')}"
def build_version = System.getenv('BUILD_NUMBER') == null ? System.currentTimeMillis() : System.getenv('BUILD_NUMBER')
project.version="${software_version}${rc_version}+${build_version}"

dependencyLocking {
    lockAllConfigurations()
}

dependencies {

    ////// Begin - Vital dependencies for Base Connector
    api 'org.apache.commons:commons-lang3:3.15.0'
    api 'net.tirasa.connid:connector-test-common:1.5.2.0'
    jwtApi 'com.auth0:java-jwt:3.19.4'

    // Instead of bringing spring we will just implement the necessary dependencies
    api 'jakarta.validation:jakarta.validation-api:3.1.0'
    api 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
    api 'org.glassfish:jakarta.el:4.0.2'

    ////// End - Vital dependencies for Base Connector

    // HTTP/RESTful support
    httpApi 'com.google.code.gson:gson:2.10.1'
    httpApi 'org.apache.httpcomponents:httpclient:4.5.14'


    // PEM Private key support
    cryptographyApi 'org.bouncycastle:bcpkix-jdk18on:1.78'
    compileOnly 'org.bouncycastle:bcpkix-jdk18on:1.78'

    // SOAP support
    // implementation 'com.sun.xml.ws:jaxws-ri:2.3.2'

    testImplementation "com.exclamationlabs.connid:connector-base-test-support:${test_connector_version}-+"
    testImplementation 'net.tirasa.connid:connector-framework-internal:1.5.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.1'

    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
    implementation 'javax.cache:cache-api:1.1.1'
}



task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

task copyPom(type: Copy) {
    from 'build/publications/connector_base/pom-default.xml'
    into 'build/libs'
    rename ('pom-default.xml', "${project.name}-${project.version}.pom")
}

publishing {
    publications {
        connector_base(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom {
                name = "Connector Base: Framework"
                description = "Base Connector Framework"
                url = "https://github.com/ExclamationLabs/connector-base"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    url = "https://github.com/ExclamationLabs/connector-base/tree/master"
                }
            }
        }
    }
    repositories {
        maven {
            url "artifactregistry://us-east1-maven.pkg.dev/provision-general-operations/provision-public/"
            credentials {
                username = "${System.getenv('ARTIFACT_REGISTRY_USER')}"
                password = "${System.getenv('ARTIFACT_REGISTRY_KEY')}"
            }
        }
    }
}

spotless {
    java {
        googleJavaFormat('1.17.0')
    }
}

test {
    useJUnitPlatform() {}
}

build.dependsOn(copyPom)
copyPom.dependsOn(publishToMavenLocal)

